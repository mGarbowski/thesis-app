#### Installation ####
install:
    uv sync

#### Running the application ####

# Run the development server with auto-reload
dev:
    uv run fastapi dev

# Run the production server
prod:
    uv run uvicorn app.main:app --host 0.0.0.0 --port 8000 --log-config logging_config.yml

# Start development database
db:
    docker compose -f ../database/docker-compose.yml up -d

# Stop development database
db_stop:
    docker compose -f ../database/docker-compose.yml down

# Seed the database with initial data
seed *args:
    uv run python scripts/lfw_seed.py {{args}}


#### Testing ####

# Run the test suite
test:
    uv run pytest

# Run the test suite with coverage report
cov:
    uv run pytest --cov=app --cov-report=term-missing


#### Static analysis ####

# Format code with black
_black:
    uv run black .

# Check code formatting with black
_black_check:
    uv run black --check .

# Sort imports with isort
_isort:
    uv run isort .

# Check import sorting with isort
_isort_check:
    uv run isort . --check-only

# Type checking with pyright
_pyright_check:
    uv run pyright

# Format code and sort imports
fmt:
    just _black
    just _isort

# Check formatting and types
check:
    just _black_check
    just _isort_check
    just _pyright_check


#### Code metrics ####

# Raw code metrics for the app directory
metrics_raw_app:
    uv run radon raw app --summary
    
# Raw code metrics for the tests directory
metrics_raw_tests:
    uv run radon raw tests --summary

# Cyclomatic complexity for the app directory
metrics_cc_app:
    uv run radon cc app --average

# Cyclomatic complexity for the tests directory
metrics_cc_tests:
    uv run radon cc tests --average

# Show all code metrics
metrics_all:
    just metrics_raw_app
    just metrics_raw_tests
    just metrics_cc_app
    just metrics_cc_tests
